FINISH
/CLEAR

D1=30
D2=17
H1=25
H2=10
H3=40
R=24
E=2e5
MU=0.3

/prep7
!Параллелепипеды
!ET,1,SOLID186
!Тэтрайдеры (10 узлов)
ET,1,SOLID187

MP,EX,1,E
MP,PRXY,1,MU

K,1,0,0,0
K,2,R/2,0,0
!Поставим точку в цилиндрической системе координат (R,phi,Z)
CSYS,1
K,4,R/2,60,0
!Операция копирования
KGEN,2,2,,,(D1-R)/2
KGEN,2,4,,,(D1-R)/2

!Эквивалентно двум командам выше
!KGEN,2,2,4,2,(D1-R)/2

L,3,5 !Будет строить линию в полярной СК, получится дуга

!Возвращаемся в декартовую СК
CSYS,0
L,1,2
L,1,4
L,4,5
L,2,3
L,2,4

/PNUM,LINE,1
GPLOT

!Площадь через линии
AL,2,3,6
AL,1,4,5,6

/PNUM,AREA,1 !Включаем нумерацию площадей
GPLOT

!Переходим в полярную СК и копируем площади
CSYS,1
AGEN,6,ALL,,,,60 !Каждые 60 градусов без шага
CSYS,0

!Операция слияния двойных объектов
NUMMRG,ALL
!Сплющивает нумерацию (чтобы не было пропусков после слияния)
NUMCMP,ALL

K,14,0,0,H2
K,15,0,0,H2+H3
L,14,15

!Способы выдавливания
!Первый способ (вдоль координат)
!Выбираем четные номера площадей
ASEL,S,,,2,12,2 !С 2 по 12 с шагом 2
VEXT,ALL,,,0,0,-H1 !Вытягиваем все выделенные площади
ALLSEL
!VEXT,2,12,2,0,0,-H1
VEXT,1,12,1,0,0,H2,D2/D1,D2/D1 !Чтобы получить конус вводим отношение начального и конечного размера

!Второй способ (выдавливание по траектории)
!Выбираем все площади, у которых координата z=H2
ASEL,S,LOC,Z,H2
VDRAG,ALL,,,,,,25
ALLSEL

NUMMRG, ALL !Аналогично после копирования - убрать повторные объекты

!Разобьем на кубики
ASIZE,ALL,D2/5

!MSHAPE,0,3D
!MSHKEY,1
!VMESH,ALL

MSHAPE,1,3D
MSHKEY,0
VMESH,ALL

